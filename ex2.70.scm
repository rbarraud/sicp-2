;;Need to do #lang scheme in scheme buffer
(define (make-leaf symbol weight)
  (list 'leaf symbol weight))

(define (leaf? object)
  (eq? (car object) 'leaf))

(define (symbol-leaf x) (cadr x))

(define (weight-leaf x) (caddr x))

(define (make-code-tree left right)
  (list left
        right
        (append (symbols left) (symbols right))
        (+ (weight left) (weight right))))

(define (left-branch tree) (car tree))

(define (right-branch tree) (cadr tree))

(define (symbols tree)
  (if (leaf? tree)
      (list (symbol-leaf tree))
      (caddr tree)))

(define (weight tree)
  (if (leaf? tree)
      (weight-leaf tree)
      (cadddr tree)))

(define (decode bits tree)
  (define (decode-1 bits current-branch)
    (if (null? bits)
        '()
        (let ((next-branch
               (choose-branch (car bits) current-branch)))
          (if (leaf? next-branch)
              (cons (symbol-leaf next-branch)
                    (decode-1 (cdr bits) tree))
              (decode-1 (cdr bits) next-branch)))))
  (decode-1 bits tree))

(define (choose-branch bit branch)
  (cond ((= bit 0) (left-branch branch))
        ((= bit 1) (right-branch branch))
        (else (error "bad bit -- CHOOSE-BRANCH" bit))))

(define (adjoin-set x set)
  (cond ((null? set) (list x))
        ((< (weight x) (weight (car set))) (cons x set))
        (else (cons (car set)
                    (adjoin-set x (cdr set))))))

(define (make-leaf-set pairs)
  (if (null? pairs)
      '()
      (let ((pair (car pairs)))
        (adjoin-set (make-leaf (car pair)   ;symbol
                               (cadr pair)) ;frequency
                    (make-leaf-set (cdr pairs))))))

(define sample-tree
  (make-code-tree (make-leaf 'A 4)
                  (make-code-tree
                   (make-leaf 'B 2)
                   (make-code-tree (make-leaf 'D 1)
                                   (make-leaf 'C 1)))))

(define sample-message '(0 1 1 0 0 1 0 1 0 1 1 1 0))
(define sample-symbols '(A D A B B C A))

(decode sample-message sample-tree)

(define (encode message tree)
  (if (null? message)
      '()
      (append (encode-symbol (car message) tree)
              (encode (cdr message) tree))))

(define (element-of-set? x set)
  (cond ((null? set) false)
        ((eq? x (car set)) true)
        (else (element-of-set? x (cdr set)))))


(define (encode-symbol symbol tree)
  (if (leaf? tree)
      '()
      (let ((left (left-branch tree))
            (right (right-branch tree)))
        (cond ((element-of-set? symbol (symbols left))
               (cons 0 (encode-symbol symbol left)))
              ((element-of-set? symbol (symbols right))
               (cons 1 (encode-symbol symbol right)))
              (else
               (error "bad symbol -- ENCODE-SYMBOL" symbol))))))


;;(encode sample-symbols sample-tree)

(define (generate-huffman-tree pairs)
  (successive-merge (make-leaf-set pairs)))
  

(define (successive-merge leaf-set)
  (if (null? (cdr leaf-set))
      (car leaf-set)
      (successive-merge (adjoin-set
                         (make-code-tree (car leaf-set)
                                         (cadr leaf-set))
                         (cddr leaf-set)))))



(define sample-weights '((A 4) (B 2) (C 1) (D 1)))

(generate-huffman-tree sample-weights)

(decode sample-message
        (generate-huffman-tree sample-weights))

(define rock-weights '((A 2) (BOOM 1) (GET 2) (JOB 2)
                       (NA 16) (SHA 3) (YIP 9) (WAH 1)))
(define rock-message
  '(GET A JOB
        SHA NA NA NA NA NA NA NA NA
        GET A JOB
        SHA NA NA NA NA NA NA NA NA
        WAH YIP YIP YIP YIP YIP YIP YIP YIP YIP
        SHA BOOM))

(length rock-message)

(generate-huffman-tree rock-weights)
(length (encode
         rock-message
         (generate-huffman-tree rock-weights)))

;;Using a fixed-length alphabet, we need 3 bits
;;(log2 8 == 3)
;;The message is 36 symbols, so 108 bits would be needed
;;The huffman encoded message is 84 bits.
